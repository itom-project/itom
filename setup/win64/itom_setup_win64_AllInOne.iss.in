; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

; NOCH:
; - Autovervollständigungshilfedateien

; Ggf. zu ändern!
#define SourceDirSys32 "C:\Windows\System32"
#define SourceDirItomSource "${CMAKE_CURRENT_SOURCE_DIR}"
#define SourceDirItomBuild "${CMAKE_CURRENT_BINARY_DIR}"
#define SourceDirQT "${QT_BINARY_DIR}"
#define SourceDirOpenCV "${OpenCV_BIN_DIR}"
#define SourceRedistributable "W:\m\ITOM\Installationen\1. MS VisualStudio 2010 Pro"
#define SourceDirPython "W:\m\ITOM\Installationen\5. Python 3.3\64 Bit"
#define SourceDirPCL "${PCL_DIR}\.."
#define ExeName "Qitom.exe"
#define Name "itom (x64)"
#define Version "${ITOM_versionString}"
#define Publisher "ITO Uni Stuttgart"
#define URL "http://www.ito.uni-stuttgart.de/"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{BC92C903-FB9E-49ED-AF5C-F43F37D3974A}
AppName={#Name}
AppVersion={#Version}
AppVerName={#Name} {#Version}
AppPublisher={#Publisher}
AppPublisherURL={#URL}
AppSupportURL={#URL}
AppUpdatesURL={#URL}
DefaultDirName={pf64}\{#Name}
DefaultGroupName={#Name}
OutputBaseFilename=setup
Compression=lzma
SolidCompression=yes

LicenseFile={#SourceDirItomSource}\COPYING.txt

[Dirs]
Name: "{app}\"; Permissions: everyone-modify
Name: "{app}\plugins"; Permissions: everyone-modify
Name: "{app}\plugins\demoAlgorithms"; Permissions: everyone-modify
Name: "{app}\plugins\demoAlgorithms\translation"; Permissions: everyone-modify
;Name: "{app}\plugins\DummyGrabber"; Permissions: everyone-modify
;Name: "{app}\plugins\DummyGrabber\translation"; Permissions: everyone-modify
;Name: "{app}\plugins\DummyMotor"; Permissions: everyone-modify
;Name: "{app}\plugins\DummyMotor\translation"; Permissions: everyone-modify
Name: "{app}\designer"; Permissions: everyone-modify
Name: "{app}\designer\translation"; Permissions: everyone-modify
;Name: "{app}\scripts"; Permissions: everyone-modify
;Name: "{app}\demoScripts"; Permissions: everyone-modify
Name: "{app}\demo"; Permissions: everyone-modify
;Name: "{app}\documentation"; Permissions: everyone-modify
Name: "{app}\itomSettings"; Permissions: everyone-modify
Name: "{app}\lib"; Permissions: everyone-modify
Name: "{app}\help"; Permissions: everyone-modify
Name: "{app}\sqldrivers"; Permissions: everyone-modify
Name: "{app}\imageformats"; Permissions: everyone-modify
Name: "{app}\translation"; Permissions: everyone-modify
Name: "{app}\itom-packages"; Permissions: everyone-modify
Name: "{app}\SDK"; Permissions: everyone-modify

[Languages]
Name: "Deutsch"; MessagesFile: "compiler:Languages\German.isl"
Name: "English"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "{#SourceDirItomBuild}\{#ExeName}"; DestDir: "{app}"; Flags: promptifolder

; DLLs
Source: "{#SourceDirItomBuild}\dataobject.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\itomCommonLib.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\itomCommonQtLib.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\itomWidgets.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\pointcloud.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\qpropertyeditor.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\phonon4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtCLucene4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtCore4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtDeclarative4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtDesigner4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtDesignerComponents4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtGui4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtHelp4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtMultimedia4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtNetwork4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtOpenGL4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtScript4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtScriptTools4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtSql4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtSvg4.dll"; DestDir: "{app}"; Flags: promptifolder
;Source: "{#SourceDirQT}\QtTest4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtWebKit4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtXml4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtXmlPatterns4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\qscintilla2.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirOpenCV}\opencv_core${OpenCV_LIB_VERSION}.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirPCL}\bin\pcl_common_release.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\..\plugins\sqldrivers\qsqlite4.dll"; DestDir: "{app}\sqldrivers"; Flags: promptifolder
Source: "{#SourceDirQT}\..\plugins\imageformats\qgif4.dll"; DestDir: "{app}\imageformats"; Flags: promptifolder
Source: "{#SourceDirQT}\..\plugins\imageformats\qico4.dll"; DestDir: "{app}\imageformats"; Flags: promptifolder
Source: "{#SourceDirQT}\..\plugins\imageformats\qicod4.dll"; DestDir: "{app}\imageformats"; Flags: promptifolder
Source: "{#SourceDirQT}\..\plugins\imageformats\qjpeg4.dll"; DestDir: "{app}\imageformats"; Flags: promptifolder
Source: "{#SourceDirQT}\..\plugins\imageformats\qmng4.dll"; DestDir: "{app}\imageformats"; Flags: promptifolder
Source: "{#SourceDirQT}\..\plugins\imageformats\qsvg4.dll"; DestDir: "{app}\imageformats"; Flags: promptifolder
Source: "{#SourceDirQT}\..\plugins\imageformats\qtga4.dll"; DestDir: "{app}\imageformats"; Flags: promptifolder
Source: "{#SourceDirQT}\..\plugins\imageformats\qtgad4.dll"; DestDir: "{app}\imageformats"; Flags: promptifolder
Source: "{#SourceDirQT}\..\plugins\imageformats\qtiff4.dll"; DestDir: "{app}\imageformats"; Flags: promptifolder

; Python-Scripts
Source: "{#SourceDirItomBuild}\itoDebugger.py"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\itoFunctions.py"; DestDir: "{app}"; Flags: promptifolder

; Hilfe
Source: "{#SourceDirItomBuild}\docs\userDoc\build\qthelp\*.qch"; DestDir: "{app}\docs\userDoc\build\qthelp"; Flags: promptifolder
Source: "{#SourceDirQT}\assistant.exe"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\qhelpgenerator.exe"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\qcollectiongenerator.exe"; DestDir: "{app}"; Flags: promptifolder

; Sprachen
Source: "{#SourceDirItomBuild}\translation\*.qm"; DestDir: "{app}\translation"; Flags: promptifolder
Source: "{#SourceDirQT}\..\translations\qt_??.qm"; DestDir: "{app}\translation"; Flags: promptifolder

; EXEs
Source: "{#SourceDirQT}\assistant.exe"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\designer.exe"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceRedistributable}\vcredist_x64.exe"; DestDir: "{tmp}"
Source: "{#SourceDirPython}\1. python-3.3.0.amd64.msi"; DestDir: "{tmp}"
Source: "{#SourceDirPython}\2. numpy-MKL-1.8.1.win-amd64-py3.3.exe"; DestDir: "{tmp}"
Source: "{#SourceDirPython}\3. scipy-0.14.0.win-amd64-py3.3.exe"; DestDir: "{tmp}"
Source: "{#SourceDirPython}\4. pyparsing-2.0.0.win-amd64-py3.3.exe"; DestDir: "{tmp}"
Source: "{#SourceDirPython}\5. python-dateutil-2.2.win-amd64-py3.3.exe"; DestDir: "{tmp}"
Source: "{#SourceDirPython}\6. six-1.6.1.win-amd64-py3.3.exe"; DestDir: "{tmp}"
Source: "{#SourceDirPython}\7. matplotlib-1.3.1.win-amd64-py3.3.exe"; DestDir: "{tmp}"
Source: "{#SourceDirPython}\8. Pillow-2.0.0.win-amd64-py3.3.exe"; DestDir: "{tmp}"

; vielleicht
Source: "{#SourceDirQT}\uic.exe"; DestDir: "{app}"; Flags: promptifolder  

; Default INI
Source: "{#SourceDirItomBuild}\itomSettings\itomDefault.ini"; DestDir: "{app}\itomSettings"; Flags: promptifolder  

; Lizenz-Datei
Source: "{#SourceDirItomSource}\Qitom\COPYING.txt"; DestDir: "{app}"; Flags: promptifolder

; itom-packages
Source: "{#SourceDirItomBuild}\docs\pluginDoc\template\*.*"; DestDir: "{app}\docs\pluginDoc\template"; Flags: promptifolder recursesubdirs
Source: "{#SourceDirItomBuild}\demo\*.*"; DestDir: "{app}\demo"; Flags: promptifolder recursesubdirs
Source: "{#SourceDirItomBuild}\itom-packages\*.*"; DestDir: "{app}\itom-packages"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\itom-packages\api_generator\*.*"; DestDir: "{app}\itom-packages\api_generator"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\itom-packages\abstractObjToolbar\*.*"; DestDir: "{app}\itom-packages\abstractObjToolbar"; Flags: promptifolder
;Source: "{#SourceDirItomBuild}\itom-packages\autostart\*.*"; DestDir: "{app}\itom-packages\autostart"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\itom-packages\camToolbar\*.*"; DestDir: "{app}\itom-packages\camToolbar"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\itom-packages\measurementSystem\*.*"; DestDir: "{app}\itom-packages\measurementSystem"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\itom-packages\mpl_itom\*.*"; DestDir: "{app}\itom-packages\mpl_itom"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\itom-packages\numpy_utils\*.*"; DestDir: "{app}\itom-packages\numpy_utils"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\itom-packages\plotToolBar\*.*"; DestDir: "{app}\itom-packages\plotToolBar"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\itom-packages\SurfaceCharakteristics\*.*"; DestDir: "{app}\itom-packages\SurfaceCharakteristics"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\SDK\*.*"; DestDir: "{app}\SDK"; Flags: promptifolder; Components: SDK
Source: "{#SourceDirItomBuild}\SDK\include\*.*"; DestDir: "{app}\SDK\include"; Flags: promptifolder recursesubdirs; Components: SDK
Source: "{#SourceDirItomBuild}\SDK\docs\*.*"; DestDir: "{app}\SDK\docs"; Flags: promptifolder recursesubdirs; Components: SDK
Source: "{#SourceDirItomBuild}\SDK\pluginTemplates\*.*"; DestDir: "{app}\SDK\pluginTemplates"; Flags: promptifolder recursesubdirs; Components: SDK
Source: "{#SourceDirItomBuild}\SDK\lib\vc10_x64\*.*"; DestDir: "{app}\SDK\lib\vc10_x64"; Components: SDK;  Flags: promptifolder

; DesignerPlugins
Source: "{#SourceDirItomBuild}\designer\dataObjectTable.dll"; DestDir: "{app}\designer"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\designer\evaluateGeometrics.dll"; DestDir: "{app}\designer"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\designer\itom1DQwtPlot.dll"; DestDir: "{app}\designer"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\designer\itom2dQwtPlot.dll"; DestDir: "{app}\designer"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\designer\itomGraphicView.dll"; DestDir: "{app}\designer"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\designer\itomIsoGLFigurePlugin.dll"; DestDir: "{app}\designer"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\designer\itomWidgets.dll"; DestDir: "{app}\designer"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\designer\matplotlibPlot.dll"; DestDir: "{app}\designer"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\designer\motorController.dll"; DestDir: "{app}\designer"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\designer\translation\*.qm"; DestDir: "{app}\designer\translation"; Flags: promptifolder recursesubdirs

; Plugins
Source: "{#SourceDirItomBuild}\plugins\AerotechEnsemble\*.*"; DestDir: "{app}\plugins\AerotechEnsemble"; Flags: promptifolder recursesubdirs
Source: "{#SourceDirItomBuild}\plugins\BasicFilters\*.*"; DestDir: "{app}\plugins\BasicFilters"; Flags: promptifolder recursesubdirs
; Source: "{#SourceDirItomBuild}\plugins\BasicGPLFilters\*.*"; DestDir: "{app}\plugins\BasicGPLFilters"; Flags: promptifolder recursesubdirs ;GPL (no included in setup)
Source: "{#SourceDirItomBuild}\plugins\cmu1394\*.*"; DestDir: "{app}\plugins\cmu1394"; Flags: promptifolder recursesubdirs
Source: "{#SourceDirItomBuild}\plugins\dataobjectarithmetic\*.*"; DestDir: "{app}\plugins\dataobjectarithmetic"; Flags: promptifolder recursesubdirs
Source: "{#SourceDirItomBuild}\plugins\DataObjectIO\*.*"; DestDir: "{app}\plugins\DataObjectIO"; Flags: promptifolder recursesubdirs
Source: "{#SourceDirItomBuild}\plugins\dispWindow\*.*"; DestDir: "{app}\plugins\dispWindow"; Flags: promptifolder recursesubdirs
Source: "{#SourceDirItomBuild}\plugins\DummyGrabber\*.*"; DestDir: "{app}\plugins\DummyGrabber"; Flags: promptifolder recursesubdirs
Source: "{#SourceDirItomBuild}\plugins\DummyMotor\*.*"; DestDir: "{app}\plugins\DummyMotor"; Flags: promptifolder recursesubdirs
; Source: "{#SourceDirItomBuild}\plugins\FFTWfilters\*.*"; DestDir: "{app}\plugins\FFTWfilters"; Flags: promptifolder recursesubdirs ;GPL (no included in setup)
Source: "{#SourceDirItomBuild}\plugins\FileGrabber\*.*"; DestDir: "{app}\plugins\FileGrabber"; Flags: promptifolder recursesubdirs
Source: "{#SourceDirItomBuild}\plugins\FireGrabber\*.*"; DestDir: "{app}\plugins\FireGrabber"; Flags: promptifolder recursesubdirs
Source: "{#SourceDirItomBuild}\plugins\FittingFilters\*.*"; DestDir: "{app}\plugins\FittingFilters"; Flags: promptifolder recursesubdirs
Source: "{#SourceDirItomBuild}\plugins\FringeProj\*.*"; DestDir: "{app}\plugins\FringeProj"; Flags: promptifolder recursesubdirs
Source: "{#SourceDirItomBuild}\plugins\GWInstekPSP\*.*"; DestDir: "{app}\plugins\GWInstekPSP"; Flags: promptifolder recursesubdirs
Source: "{#SourceDirItomBuild}\plugins\LeicaMotorFocus\*.*"; DestDir: "{app}\plugins\LeicaMotorFocus"; Flags: promptifolder recursesubdirs
Source: "{#SourceDirItomBuild}\plugins\MSMediaFoundation\*.*"; DestDir: "{app}\plugins\MSMediaFoundation"; Flags: promptifolder recursesubdirs
Source: "{#SourceDirItomBuild}\plugins\OpenCVFilters\*.*"; DestDir: "{app}\plugins\OpenCVFilters"; Flags: promptifolder recursesubdirs
Source: "{#SourceDirItomBuild}\plugins\OpenCVGrabber\*.*"; DestDir: "{app}\plugins\OpenCVGrabber"; Flags: promptifolder recursesubdirs
Source: "{#SourceDirItomBuild}\plugins\PclTools\*.*"; DestDir: "{app}\plugins\PclTools"; Flags: promptifolder recursesubdirs
Source: "{#SourceDirItomBuild}\plugins\PCOCamera\*.*"; DestDir: "{app}\plugins\PCOCamera"; Flags: promptifolder recursesubdirs
Source: "{#SourceDirItomBuild}\plugins\PCOPixelFly\*.*"; DestDir: "{app}\plugins\PCOPixelFly"; Flags: promptifolder recursesubdirs
Source: "{#SourceDirItomBuild}\plugins\PGRFlyCapture\*.*"; DestDir: "{app}\plugins\PGRFlyCapture"; Flags: promptifolder recursesubdirs
Source: "{#SourceDirItomBuild}\plugins\PIPiezoCtrl\*.*"; DestDir: "{app}\plugins\PIPiezoCtrl"; Flags: promptifolder recursesubdirs
Source: "{#SourceDirItomBuild}\plugins\QCam\*.*"; DestDir: "{app}\plugins\QCam"; Flags: promptifolder recursesubdirs
Source: "{#SourceDirItomBuild}\plugins\SerialIO\*.*"; DestDir: "{app}\plugins\SerialIO"; Flags: promptifolder recursesubdirs
Source: "{#SourceDirItomBuild}\plugins\USBMotion3XIII\*.*"; DestDir: "{app}\plugins\USBMotion3XIII"; Flags: promptifolder recursesubdirs
Source: "{#SourceDirItomBuild}\plugins\Vistek\*.*"; DestDir: "{app}\plugins\Vistek"; Flags: promptifolder recursesubdirs
Source: "{#SourceDirItomBuild}\plugins\x3pio\*.*"; DestDir: "{app}\plugins\x3pio"; Flags: promptifolder recursesubdirs
Source: "{#SourceDirItomBuild}\plugins\Ximea\*.*"; DestDir: "{app}\plugins\Ximea"; Flags: promptifolder recursesubdirs

; Libs
Source: "{#SourceDirOpenCV}\opencv_calib3d${OpenCV_LIB_VERSION}.dll"; DestDir: "{app}\lib"; Flags: promptifolder
Source: "{#SourceDirOpenCV}\opencv_highgui${OpenCV_LIB_VERSION}.dll"; DestDir: "{app}\lib"; Flags: promptifolder
Source: "{#SourceDirOpenCV}\opencv_imgproc${OpenCV_LIB_VERSION}.dll"; DestDir: "{app}\lib"; Flags: promptifolder
Source: "{#SourceDirOpenCV}\opencv_features2d${OpenCV_LIB_VERSION}.dll"; DestDir: "{app}\lib"; Flags: promptifolder
Source: "{#SourceDirOpenCV}\opencv_flann${OpenCV_LIB_VERSION}.dll"; DestDir: "{app}\lib"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\lib\1394camera.dll"; DestDir: "{app}\lib"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\lib\AeroBasic64.dll"; DestDir: "{app}\lib"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\lib\EnsembleC64.dll"; DestDir: "{app}\lib"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\lib\EnsembleCore64.dll"; DestDir: "{app}\lib"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\lib\FlyCapture2_v100.dll"; DestDir: "{app}\lib"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\lib\glew32.dll"; DestDir: "{app}\lib"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\lib\iso5436-2-xml64.dll"; DestDir: "{app}\lib"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\lib\libiomp5md.dll"; DestDir: "{app}\lib"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\lib\m3apiX64.dll"; DestDir: "{app}\lib"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\lib\SC2_Cam.dll"; DestDir: "{app}\lib"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\lib\SVGigE.x64.dll"; DestDir: "{app}\lib"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\lib\SVGigETLFilter.x64.dll"; DestDir: "{app}\lib"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\lib\SVGigETLWinsock.x64.dll"; DestDir: "{app}\lib"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\lib\xiApi.NETX64.dll"; DestDir: "{app}\lib"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\lib\iso5436_2.xsd"; DestDir: "{app}\lib"; Flags: promptifolder
Source: "{#SourceDirPCL}\bin\pcl_io_release.dll"; DestDir: "{app}\lib"; Flags: promptifolder
Source: "{#SourceDirPCL}\bin\pcl_kdtree_release.dll"; DestDir: "{app}\lib"; Flags: promptifolder
Source: "{#SourceDirPCL}\bin\pcl_search_release.dll"; DestDir: "{app}\lib"; Flags: promptifolder
Source: "{#SourceDirPCL}\bin\pcl_features_release.dll"; DestDir: "{app}\lib"; Flags: promptifolder
Source: "{#SourceDirPCL}\bin\pcl_filters_release.dll"; DestDir: "{app}\lib"; Flags: promptifolder
Source: "{#SourceDirPCL}\bin\pcl_surface_release.dll"; DestDir: "{app}\lib"; Flags: promptifolder

; Demo
Source: "{#SourceDirItomSource}\demo\*.*"; DestDir: "{app}\demo"; Flags: promptifolder recursesubdirs

[Icons]
Name: "{group}\{#Name}"; Filename: "{app}\{#ExeName}"
Name: "{commondesktop}\{#Name}"; Filename: "{app}\{#ExeName}"; Tasks: desktopicon

[Run]
Filename: "{tmp}\vcredist_x64.exe"; Flags: shellexec waituntilterminated; StatusMsg: "Microsoft Visual C++ 2010 Redistributable Package (x64)..."; Components: CREDPACK;
Filename: "{tmp}\1. python-3.3.0.amd64.msi"; Flags: shellexec waituntilterminated; StatusMsg: "Phyton 3.3..."; Components: PYTHON
Filename: "{tmp}\2. numpy-MKL-1.8.1.win-amd64-py3.3.exe"; Flags: shellexec waituntilterminated; StatusMsg: "NumPy..."; Components: NUMPY
Filename: "{tmp}\3. scipy-0.14.0.win-amd64-py3.3.exe"; Flags: shellexec waituntilterminated; StatusMsg: "SciPy..."; Components: SCIPY
Filename: "{tmp}\4. pyparsing-2.0.0.win-amd64-py3.3.exe"; Flags: shellexec waituntilterminated; StatusMsg: "PyParsing..."; Components: PYPARSING
Filename: "{tmp}\5. python-dateutil-2.2.win-amd64-py3.3.exe"; Flags: shellexec waituntilterminated; StatusMsg: "DateUtil..."; Components: DATEUTIL
Filename: "{tmp}\6. six-1.6.1.win-amd64-py3.3.exe"; Flags: shellexec waituntilterminated; StatusMsg: "DateUtil..."; Components: SIX
Filename: "{tmp}\7. matplotlib-1.3.1.win-amd64-py3.3.exe"; Flags: shellexec waituntilterminated; StatusMsg: "MatPlotLib..."; Components: MATPLOTLIB
Filename: "{tmp}\8. Pillow-2.0.0.win-amd64-py3.3.exe"; Flags: shellexec waituntilterminated; StatusMsg: "Pillow..."; Components: PILLOW
Filename: "{app}\{#ExeName}"; Flags: nowait postinstall skipifsilent; Description: "{cm:LaunchProgram,{#StringChange(Name, "&", "&&")}}"; Components: Main

[InstallDelete]
Type: files; Name: "{tmp}\1. python-3.3.0.amd64.msi"
Type: files; Name: "{tmp}\2. numpy-MKL-1.8.1.win-amd64-py3.3.exe"
Type: files; Name: "{tmp}\3. scipy-0.14.0.win-amd64-py3.3.exe"
Type: files; Name: "{tmp}\4. pyparsing-2.0.0.win-amd64-py3.3.exe"
Type: files; Name: "{tmp}\5. python-dateutil-2.2.win-amd64-py3.3.exe"
Type: files; Name: "{tmp}\6. six-1.6.1.win-amd64-py3.3.exe"
Type: files; Name: "{tmp}\7. matplotlib-1.3.1.win-amd64-py3.3.exe"
Type: files; Name: "{tmp}\8. Pillow-2.0.0.win-amd64-py3.3.exe"
Type: files; Name: "{tmp}\vcredist_x64.exe"

[Types]
Name: "Install"; Description: "Installation"; Flags: iscustom

[Components]
Name: "Main"; Description: "{#Name}"; Types: Install; Flags: checkablealone fixed
Name: "CREDPACK"; Description: "Microsoft Visual C++ 2010 Redistributable Package (x64)"; Types: Install; Flags: checkablealone fixed; Check: NeedInstallRP10
Name: "PYTHON"; Description: "Python 3.3.0"; Types: Install; Flags: checkablealone
Name: "NUMPY"; Description: "numpy-MKL-1.8.1.win-amd64-py3.3"; Types: Install; Flags: checkablealone
Name: "SCIPY"; Description: "scipy-0.14.0.win-amd64-py3.3"; Types: Install; Flags: checkablealone
Name: "PYPARSING"; Description: "pyparsing-2.0.0.win-amd64-py3.3"; Types: Install; Flags: checkablealone
Name: "DATEUTIL"; Description: "python-dateutil-2.2.win-amd64-py3.3"; Types: Install; Flags: checkablealone
Name: "SIX"; Description: "six-1.6.1.win-amd64-py3.3"; Types: Install; Flags: checkablealone
Name: "MATPLOTLIB"; Description: "matplotlib-1.3.1.win-amd64-py3.3"; Types: Install; Flags: checkablealone
Name: "PILLOW"; Description: "Pillow-2.0.0.win-amd64-py3.3"; Types: Install; Flags: checkablealone
Name: "SDK"; Description: "itom SDK"; Types: Install; Flags: checkablealone

[UninstallDelete]
Type: filesandordirs; Name: "{app}\{#ExeName}"

[Code]
function MsiQueryProductState(ProductCode: string): integer;
  external 'MsiQueryProductStateA@msi.dll stdcall';

const
  INSTALLSTATE_DEFAULT = 5;
  INSTALLLEVEL_MAXIMUM = $ffff;
  INSTALLSTATE_ABSENT = 2;
  RP86 =    '{196BB40D-1578-3D01-B289-BEFC77A11A1E}';  // Visual C++ 2010 Redistributable Package (x86)
  RP64 =    '{DA5E371C-6333-3D8A-93A4-6FD5B20BCC6E}';  // Visual C++ 2010 Redistributable Package (x64)
  RPIA =    '{C1A35166-4301-38E9-BA67-02823AD72A1B}';  // Visual C++ 2010 Redistributable Package (ia64) 
  RP86SP1 = '{F0C3E5D1-1ADE-321E-8167-68EF0DE699A5}';  // Visual C++ 2010 SP1 Redistributable Package (x86)
  RP64SP1 = '{1D8E6291-B0D5-35EC-8441-6616F567A0F7}';  // Visual C++ 2010 SP1 Redistributable Package (x64)
  RPIASP1 = '{88C73C1C-2DE5-3B01-AFB8-B46EF4AB41CD}';  // Visual C++ 2010 SP1 Redistributable Package (ia64)

function IsMsiProductInstalled(const ProductId: string): Boolean;
begin
  Result:= MsiQueryProductState(ProductId) = INSTALLSTATE_DEFAULT;
end;

function NeedInstallRP10(): Boolean;
begin
  Result:= not (IsMsiProductInstalled(RP64) or IsMsiProductInstalled(RP64SP1));
//                IsMsiProductInstalled(RP86) or IsMsiProductInstalled(RP86SP1));
//                IsMsiProductInstalled(RPIA) or IsMsiProductInstalled(RPIASP1));
end;

procedure DeleteDirAndFiles(const Path, Filename: String; const IsDir, DeleteFiles, DeleteSubdirsAlso: Boolean);
var  Filter: string;
begin
  if DirExists(Path) then begin
    Filter := Path;
    if Filename <> '' then begin
      if Copy(Filter, Length(Filter), 1)<>'\' then  Filter := Filter + '\';    
      Filter := Filter + Filename;
    end; 
    DelTree(Filter, IsDir, DeleteFiles, DeleteSubdirsAlso);
  end 
end;

procedure CurStepChanged(CurStep: TSetupStep);
var  AppDir, MsgText: string;
begin
  if (CurStep = ssInstall) then begin
    AppDir := ExpandConstant('{app}') + '\';
    if DirExists(AppDir + 'SDK') or DirExists(AppDir + 'lib') or DirExists(AppDir + 'plugins') or DirExists(AppDir + 'designer') then begin
      MsgText := 'This selected application directory contains at least one of the folders "designer", "plugins", "help" or "SDK". For a proper installation of the '+
                 'current itom version these folder should be cleared. Do you want this setup to clear these folders before starting the installation?';
      if MsgBox(MsgText, mbConfirmation, MB_YESNO) = IDYES then begin
        DeleteDirAndFiles(AppDir + 'SDK', '', True, True, True);
        DeleteDirAndFiles(AppDir + 'translation', '', True, True, True);
        DeleteDirAndFiles(AppDir + 'imageformats', '', True, True, True);
        DeleteDirAndFiles(AppDir + 'sqldrivers', '', True, True, True);
        DeleteDirAndFiles(AppDir + 'help', '', True, True, True);
        DeleteDirAndFiles(AppDir + 'lib', '', True, True, True);
        DeleteDirAndFiles(AppDir + 'designer', '', True, True, True);
        DeleteDirAndFiles(AppDir + 'plugins', '', True, True, True);
        DeleteDirAndFiles(AppDir + 'docs\userDoc\build\qthelp', '', True, True, True);
        DeleteDirAndFiles(AppDir, '*.dll', False, True, False);
      end;
    end;
  end;
end;

