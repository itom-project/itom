; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

; NOCH:
; - Autovervollständigungshilfedateien

; Ggf. zu ändern!
#define SourceDirSys32 "C:\Windows\System32"
#define SourceDirItomSource "${CMAKE_CURRENT_SOURCE_DIR}"
#define SourceDirItomBuild "${CMAKE_CURRENT_BINARY_DIR}"
#define SourceDirQT "${QT_BINARY_DIR}"
#define SourceDirOpenCV "${OpenCV_BIN_DIR}"
#define SourceDirPCL "${PCL_DIR}\.."
#define ExeName "Qitom.exe"
#define Name "itom"
#define Version "${ITOM_versionString}"
#define Publisher "ITO Uni Stuttgart"
#define URL "http://www.ito.uni-stuttgart.de/"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{049B12B4-806D-43A2-9616-54FF0F889B30}
AppName={#Name}
AppVersion={#Version}
AppVerName={#Name} {#Version}
AppPublisher={#Publisher}
AppPublisherURL={#URL}
AppSupportURL={#URL}
AppUpdatesURL={#URL}
DefaultDirName={pf}\{#Name}
DefaultGroupName={#Name}
OutputBaseFilename=setup
Compression=lzma
SolidCompression=yes

LicenseFile={#SourceDirItomSource}\COPYING.txt

[Dirs]
Name: "{app}\"; Permissions: everyone-modify
Name: "{app}\plugins"; Permissions: everyone-modify
Name: "{app}\plugins\demoAlgorithms"; Permissions: everyone-modify
Name: "{app}\plugins\demoAlgorithms\translation"; Permissions: everyone-modify
Name: "{app}\plugins\DummyGrabber"; Permissions: everyone-modify
Name: "{app}\plugins\DummyGrabber\translation"; Permissions: everyone-modify
Name: "{app}\plugins\DummyMotor"; Permissions: everyone-modify
Name: "{app}\plugins\DummyMotor\translation"; Permissions: everyone-modify
Name: "{app}\designer"; Permissions: everyone-modify
Name: "{app}\designer\translation"; Permissions: everyone-modify
Name: "{app}\demo"; Permissions: everyone-modify
;Name: "{app}\scripts"; Permissions: everyone-modify
;Name: "{app}\demoScripts"; Permissions: everyone-modify
;Name: "{app}\documentation"; Permissions: everyone-modify
Name: "{app}\itomSettings"; Permissions: everyone-modify
Name: "{app}\lib"; Permissions: everyone-modify
Name: "{app}\help"; Permissions: everyone-modify
Name: "{app}\sqldrivers"; Permissions: everyone-modify
Name: "{app}\imageformats"; Permissions: everyone-modify
Name: "{app}\translation"; Permissions: everyone-modify
Name: "{app}\itom-packages"; Permissions: everyone-modify
Name: "{app}\SDK"; Permissions: everyone-modify

[Languages]
Name: "Deutsch"; MessagesFile: "compiler:Languages\German.isl"
Name: "English"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "{#SourceDirItomBuild}\{#ExeName}"; DestDir: "{app}"; Flags: promptifolder

; DLLs
Source: "{#SourceDirQT}\phonon4.dll"; DestDir: "{app}"; Flags: promptifolder
;Source: "{#SourceDirQT}\Qt3Support4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtCLucene4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtCore4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtDeclarative4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtDesigner4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtDesignerComponents4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtGui4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtHelp4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtMultimedia4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtNetwork4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtOpenGL4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtScript4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtScriptTools4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtSql4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtSvg4.dll"; DestDir: "{app}"; Flags: promptifolder
;Source: "{#SourceDirQT}\QtTest4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtWebKit4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtXml4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtXmlPatterns4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\qscintilla2.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirOpenCV}\opencv_core${OpenCV_LIB_VERSION}.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirPCL}\bin\pcl_common_release.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\..\plugins\sqldrivers\qsqlite4.dll"; DestDir: "{app}\sqldrivers"; Flags: promptifolder
Source: "{#SourceDirQT}\..\plugins\imageformats\qgif4.dll"; DestDir: "{app}\imageformats"; Flags: promptifolder
Source: "{#SourceDirQT}\..\plugins\imageformats\qico4.dll"; DestDir: "{app}\imageformats"; Flags: promptifolder
Source: "{#SourceDirQT}\..\plugins\imageformats\qicod4.dll"; DestDir: "{app}\imageformats"; Flags: promptifolder
Source: "{#SourceDirQT}\..\plugins\imageformats\qjpeg4.dll"; DestDir: "{app}\imageformats"; Flags: promptifolder
Source: "{#SourceDirQT}\..\plugins\imageformats\qmng4.dll"; DestDir: "{app}\imageformats"; Flags: promptifolder
Source: "{#SourceDirQT}\..\plugins\imageformats\qsvg4.dll"; DestDir: "{app}\imageformats"; Flags: promptifolder
Source: "{#SourceDirQT}\..\plugins\imageformats\qtga4.dll"; DestDir: "{app}\imageformats"; Flags: promptifolder
Source: "{#SourceDirQT}\..\plugins\imageformats\qtgad4.dll"; DestDir: "{app}\imageformats"; Flags: promptifolder
Source: "{#SourceDirQT}\..\plugins\imageformats\qtiff4.dll"; DestDir: "{app}\imageformats"; Flags: promptifolder

; Python-Scripts
Source: "{#SourceDirItomBuild}\itoDebugger.py"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\itoFunctions.py"; DestDir: "{app}"; Flags: promptifolder

; Hilfe
Source: "{#SourceDirItomBuild}\docs\userDoc\build\qthelp\*.qch"; DestDir: "{app}\docs\userDoc\build\qthelp"; Flags: promptifolder
Source: "{#SourceDirQT}\assistant.exe"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\qhelpgenerator.exe"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\qcollectiongenerator.exe"; DestDir: "{app}"; Flags: promptifolder

; Sprachen
Source: "{#SourceDirItomBuild}\translation\*.qm"; DestDir: "{app}\translation"; Flags: promptifolder
Source: "{#SourceDirQT}\..\translations\qt_??.qm"; DestDir: "{app}\translation"; Flags: promptifolder

; EXEs
Source: "{#SourceDirQT}\assistant.exe"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\designer.exe"; DestDir: "{app}"; Flags: promptifolder

; vielleicht
Source: "{#SourceDirQT}\uic.exe"; DestDir: "{app}"; Flags: promptifolder

; Default INI
Source: "{#SourceDirItomBuild}\itomSettings\itomDefault.ini"; DestDir: "{app}\itomSettings"; Flags: promptifolder

; Lizenz-Datei
Source: "{#SourceDirItomSource}\Qitom\COPYING.txt"; DestDir: "{app}"; Flags: promptifolder

; itom-packages
Source: "{#SourceDirItomBuild}\demo\*.*"; DestDir: "{app}\demo"; Flags: promptifolder recursesubdirs
Source: "{#SourceDirItomBuild}\itom-packages\*.*"; DestDir: "{app}\itom-packages"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\itom-packages\api_generator\*.*"; DestDir: "{app}\itom-packages\api_generator"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\itom-packages\abstractObjToolbar\*.*"; DestDir: "{app}\itom-packages\abstractObjToolbar"; Flags: promptifolder
;Source: "{#SourceDirItomBuild}\itom-packages\autostart\*.*"; DestDir: "{app}\itom-packages\autostart"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\itom-packages\camToolbar\*.*"; DestDir: "{app}\itom-packages\camToolbar"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\itom-packages\measurementSystem\*.*"; DestDir: "{app}\itom-packages\measurementSystem"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\itom-packages\mpl_itom\*.*"; DestDir: "{app}\itom-packages\mpl_itom"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\itom-packages\numpy_utils\*.*"; DestDir: "{app}\itom-packages\numpy_utils"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\itom-packages\plotToolBar\*.*"; DestDir: "{app}\itom-packages\plotToolBar"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\itom-packages\SurfaceCharakteristics\*.*"; DestDir: "{app}\itom-packages\SurfaceCharakteristics"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\SDK\*.*"; DestDir: "{app}\SDK"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\SDK\include\*.*"; DestDir: "{app}\SDK\include"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\SDK\include\common\*.*"; DestDir: "{app}\SDK\include\common"; Flags: promptifolder; Components: SDK
Source: "{#SourceDirItomBuild}\SDK\include\DataObject\*.*"; DestDir: "{app}\SDK\include\DataObject"; Flags: promptifolder; Components: SDK
Source: "{#SourceDirItomBuild}\SDK\include\PointCloud\*.*"; DestDir: "{app}\SDK\include\PointCloud"; Flags: promptifolder; Components: SDK
Source: "{#SourceDirItomBuild}\SDK\include\QPropertyEditor\*.*"; DestDir: "{app}\SDK\include\QPropertyEditor"; Flags: promptifolder; Components: SDK
Source: "{#SourceDirItomBuild}\SDK\lib\vc10_x86\*.*"; DestDir: "{app}\SDK\lib\vc10_x86"; Flags: promptifolder; Components: SDK

; DesignerPlugins
Source: "{#SourceDirItomBuild}\designer\dataObjectTable.dll"; DestDir: "{app}\designer"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\designer\evaluateGeometrics.dll"; DestDir: "{app}\designer"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\designer\itom1DQwtPlot.dll"; DestDir: "{app}\designer"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\designer\itom2dQwtPlot.dll"; DestDir: "{app}\designer"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\designer\itomGraphicView.dll"; DestDir: "{app}\designer"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\designer\itomIsoGLFigurePlugin.dll"; DestDir: "{app}\designer"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\designer\matplotlibPlot.dll"; DestDir: "{app}\designer"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\designer\motorController.dll"; DestDir: "{app}\designer"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\designer\translation\*.qm"; DestDir: "{app}\designer\translation"; Flags: promptifolder recursesubdirs

; Plugins
Source: "{#SourceDirItomBuild}\plugins\AerotechEnsemble\AerotechEnsemble.dll"; DestDir: "{app}\plugins\AerotechEnsemble"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\AerotechEnsemble\translation\*.qm"; DestDir: "{app}\plugins\AerotechEnsemble\translation"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\BasicFilters\BasicFilters.dll"; DestDir: "{app}\plugins\BasicFilters"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\BasicFilters\translation\*.qm"; DestDir: "{app}\plugins\BasicFilters\translation"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\cmu1394\cmu1394.dll"; DestDir: "{app}\plugins\cmu1394"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\cmu1394\translation\*.qm"; DestDir: "{app}\plugins\cmu1394\translation"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\dataobjectarithmetic\dataobjectarithmetic.dll"; DestDir: "{app}\plugins\dataobjectarithmetic"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\dataobjectarithmetic\translation\*.qm"; DestDir: "{app}\plugins\dataobjectarithmetic\translation"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\DataObjectIO\DataObjectIO.dll"; DestDir: "{app}\plugins\DataObjectIO"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\DataObjectIO\translation\*.qm"; DestDir: "{app}\plugins\DataObjectIO\translation"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\dispWindow\dispWindow.dll"; DestDir: "{app}\plugins\dispWindow"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\dispWindow\translation\*.qm"; DestDir: "{app}\plugins\dispWindow\translation"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\DummyGrabber\DummyGrabber.dll"; DestDir: "{app}\plugins\DummyGrabber"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\DummyGrabber\translation\*.qm"; DestDir: "{app}\plugins\DummyGrabber\translation"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\DummyMotor\DummyMotor.dll"; DestDir: "{app}\plugins\DummyMotor"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\DummyMotor\translation\*.qm"; DestDir: "{app}\plugins\DummyMotor\translation"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\FileGrabber\FileGrabber.dll"; DestDir: "{app}\plugins\FileGrabber"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\FileGrabber\translation\*.qm"; DestDir: "{app}\plugins\FileGrabber\translation"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\FireGrabber\FireGrabber.dll"; DestDir: "{app}\plugins\FireGrabber"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\FireGrabber\translation\*.qm"; DestDir: "{app}\plugins\FireGrabber\translation"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\FittingFilters\FittingFilters.dll"; DestDir: "{app}\plugins\FittingFilters"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\FittingFilters\translation\*.qm"; DestDir: "{app}\plugins\FittingFilters\translation"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\GWInstekPSP\GWInstekPSP.dll"; DestDir: "{app}\plugins\GWInstekPSP"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\GWInstekPSP\translation\*.qm"; DestDir: "{app}\plugins\GWInstekPSP\translation"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\LeicaMotorFocus\LeicaMotorFocus.dll"; DestDir: "{app}\plugins\LeicaMotorFocus"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\LeicaMotorFocus\translation\*.qm"; DestDir: "{app}\plugins\LeicaMotorFocus\translation"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\OpenCVFilters\OpenCVFilters.dll"; DestDir: "{app}\plugins\OpenCVFilters"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\OpenCVFilters\translation\*.qm"; DestDir: "{app}\plugins\OpenCVFilters\translation"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\OpenCVGrabber\OpenCVGrabber.dll"; DestDir: "{app}\plugins\OpenCVGrabber"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\OpenCVGrabber\translation\*.qm"; DestDir: "{app}\plugins\OpenCVGrabber\translation"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\PCOPixelFly\PCOPixelFly.dll"; DestDir: "{app}\plugins\PCOPixelFly"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\PCOPixelFly\translation\*.qm"; DestDir: "{app}\plugins\PCOPixelFly\translation"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\PIPiezoCtrl\PIPiezoCtrl.dll"; DestDir: "{app}\plugins\PIPiezoCtrl"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\PIPiezoCtrl\translation\*.qm"; DestDir: "{app}\plugins\PIPiezoCtrl\translation"; Flags: promptifolder
;Source: "{#SourceDirItomBuild}\plugins\QCam\QCam.dll"; DestDir: "{app}\plugins\QCam"; Flags: promptifolder
;Source: "{#SourceDirItomBuild}\plugins\QCam\translation\*.qm"; DestDir: "{app}\plugins\QCam\translation"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\SerialIO\SerialIO.dll"; DestDir: "{app}\plugins\SerialIO"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\SerialIO\translation\*.qm"; DestDir: "{app}\plugins\SerialIO\translation"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\USBMotion3XIII\USB3xIII.dll"; DestDir: "{app}\plugins\USBMotion3XIII"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\USBMotion3XIII\USBMotion3XIII.dll"; DestDir: "{app}\plugins\USBMotion3XIII"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\USBMotion3XIII\translation\*.qm"; DestDir: "{app}\plugins\USBMotion3XIII\translation"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\Vistek\Vistek.dll"; DestDir: "{app}\plugins\Vistek"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\plugins\Vistek\translation\*.qm"; DestDir: "{app}\plugins\Vistek\translation"; Flags: promptifolder

; Libs
Source: "{#SourceDirOpenCV}\opencv_calib3d${OpenCV_LIB_VERSION}.dll"; DestDir: "{app}\lib"; Flags: promptifolder
Source: "{#SourceDirOpenCV}\opencv_highgui${OpenCV_LIB_VERSION}.dll"; DestDir: "{app}\lib"; Flags: promptifolder
Source: "{#SourceDirOpenCV}\opencv_imgproc${OpenCV_LIB_VERSION}.dll"; DestDir: "{app}\lib"; Flags: promptifolder
Source: "{#SourceDirOpenCV}\opencv_features2d${OpenCV_LIB_VERSION}.dll"; DestDir: "{app}\lib"; Flags: promptifolder
Source: "{#SourceDirOpenCV}\opencv_flann${OpenCV_LIB_VERSION}.dll"; DestDir: "{app}\lib"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\lib\glew32.dll"; DestDir: "{app}\lib"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\lib\AeroBasic.dll"; DestDir: "{app}\lib"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\lib\EnsembleC.dll"; DestDir: "{app}\lib"; Flags: promptifolder
Source: "{#SourceDirItomBuild}\lib\EnsembleCore.dll"; DestDir: "{app}\lib"; Flags: promptifolder

; Demo
Source: "{#SourceDirItomSource}\demo\*.*"; DestDir: "{app}\demo"; Flags: promptifolder recursesubdirs

[Icons]
Name: "{group}\{#Name}"; Filename: "{app}\{#ExeName}"
Name: "{commondesktop}\{#Name}"; Filename: "{app}\{#ExeName}"; Tasks: desktopicon

[Run]
Filename: "{app}\{#ExeName}"; Flags: nowait postinstall skipifsilent; Description: "{cm:LaunchProgram,{#StringChange(Name, "&", "&&")}}"; Components: Main

[Types]
Name: "Install"; Description: "Installation"; Flags: iscustom

[Components]
Name: "Main"; Description: "{#Name}"; Types: Install; Flags: checkablealone fixed
Name: "SDK"; Description: "itom SDK"; Types: Install; Flags: checkablealone

[UninstallDelete]
Type: filesandordirs; Name: "{app}\{#ExeName}"

[Code]
function MsiQueryProductState(ProductCode: string): integer;
  external 'MsiQueryProductStateA@msi.dll stdcall';

const
  INSTALLSTATE_DEFAULT = 5;
  INSTALLLEVEL_MAXIMUM = $ffff;
  INSTALLSTATE_ABSENT = 2;
  RP86 =    '{196BB40D-1578-3D01-B289-BEFC77A11A1E}';  // Visual C++ 2010 Redistributable Package (x86)
  RP64 =    '{DA5E371C-6333-3D8A-93A4-6FD5B20BCC6E}';  // Visual C++ 2010 Redistributable Package (x64)
  RPIA =    '{C1A35166-4301-38E9-BA67-02823AD72A1B}';  // Visual C++ 2010 Redistributable Package (ia64) 
  RP86SP1 = '{F0C3E5D1-1ADE-321E-8167-68EF0DE699A5}';  // Visual C++ 2010 SP1 Redistributable Package (x86)
  RP64SP1 = '{1D8E6291-B0D5-35EC-8441-6616F567A0F7}';  // Visual C++ 2010 SP1 Redistributable Package (x64)
  RPIASP1 = '{88C73C1C-2DE5-3B01-AFB8-B46EF4AB41CD}';  // Visual C++ 2010 SP1 Redistributable Package (ia64)

function IsMsiProductInstalled(const ProductId: string): Boolean;
begin
  Result:= MsiQueryProductState(ProductId) = INSTALLSTATE_DEFAULT;
end;

function NeedInstallRP10(): Boolean;
begin
  Result:= not (IsMsiProductInstalled(RP86) or IsMsiProductInstalled(RP86SP1));
//                IsMsiProductInstalled(RP64) or IsMsiProductInstalled(RP64SP1)
//                IsMsiProductInstalled(RPIA) or IsMsiProductInstalled(RPIASP1));
end;

procedure DeleteDirAndFiles(const Path, Filename: String; const IsDir, DeleteFiles, DeleteSubdirsAlso: Boolean);
var  Filter: string;
begin
  if DirExists(Path) then begin
    Filter := Path;
    if Filename <> '' then begin
      if Copy(Filter, Length(Filter), 1)<>'\' then  Filter := Filter + '\';    
      Filter := Filter + Filename;
    end; 
    DelTree(Filter, IsDir, DeleteFiles, DeleteSubdirsAlso);
  end 
end;

procedure CurStepChanged(CurStep: TSetupStep);
var  AppDir, MsgText: string;
begin
  if (CurStep = ssInstall) then begin
    AppDir := ExpandConstant('{app}') + '\';
    if DirExists(AppDir + 'SDK') or DirExists(AppDir + 'lib') or DirExists(AppDir + 'plugins') or DirExists(AppDir + 'designer') then begin
      MsgText := 'This selected application directory contains at least one of the folders "designer", "plugins", "help" or "SDK". For a proper installation of the '+
                 'current itom version these folder should be cleared. Do you want this setup to clear these folders before starting the installation?';
      if MsgBox(MsgText, mbConfirmation, MB_YESNO) = IDYES then begin
        DeleteDirAndFiles(AppDir + 'SDK', '', True, True, True);
        DeleteDirAndFiles(AppDir + 'translation', '', True, True, True);
        DeleteDirAndFiles(AppDir + 'imageformats', '', True, True, True);
        DeleteDirAndFiles(AppDir + 'sqldrivers', '', True, True, True);
        DeleteDirAndFiles(AppDir + 'help', '', True, True, True);
        DeleteDirAndFiles(AppDir + 'lib', '', True, True, True);
        DeleteDirAndFiles(AppDir + 'designer', '', True, True, True);
        DeleteDirAndFiles(AppDir + 'plugins', '', True, True, True);
        DeleteDirAndFiles(AppDir + 'docs\userDoc\build\qthelp', '', True, True, True);
        DeleteDirAndFiles(AppDir, '*.dll', False, True, False);
      end;
    end;
  end;
end;

