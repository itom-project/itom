; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

; NOCH:
; - Autovervollständigungshilfedateien

; Ggf. zu ändern!
#define SourceDirSys32 "C:\Windows\System32"
#define SourceDirITOM "${CMAKE_CURRENT_BINARY_DIR}"
#define SourceDirITOMHelp "${CMAKE_CURRENT_SOURCE_DIR}"
#define SourceDirQT "${QT_BINARY_DIR}"
#define SourceDirOpenCV "${OpenCV_BIN_DIR}"
#define SourceRedistributable "W:\m\ITOM\Installationen\1. MS VisualStudio 2010 Pro"
#define SourceDirPython "W:\m\ITOM\Installationen\5. Python 3.2\32 Bit"
;#define SourceDirPCL "${PCL_DIR}/../bin"
#define ExeName "Qitom.exe"
#define Name "ITOM"
#define Version "${ITOM_versionString}"
#define Publisher "ITO Uni Stuttgart"
#define URL "http://www.ito.uni-stuttgart.de/"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{049B12B4-806D-43A2-9616-54FF0F889B30}
AppName={#Name}
AppVersion={#Version}
AppVerName={#Name} {#Version}
AppPublisher={#Publisher}
AppPublisherURL={#URL}
AppSupportURL={#URL}
AppUpdatesURL={#URL}
DefaultDirName={pf}\{#Name}
DefaultGroupName={#Name}
OutputBaseFilename=setup
Compression=lzma
SolidCompression=yes

LicenseFile={#SourceDirITOMHelp}\COPYING.txt

[Dirs]
Name: "{app}\"; Permissions: everyone-modify
Name: "{app}\plugins"; Permissions: everyone-modify
Name: "{app}\designer"; Permissions: everyone-modify
Name: "{app}\scripts"; Permissions: everyone-modify
Name: "{app}\demoScripts"; Permissions: everyone-modify
Name: "{app}\documentation"; Permissions: everyone-modify
Name: "{app}\itomSettings"; Permissions: everyone-modify
Name: "{app}\lib"; Permissions: everyone-modify
Name: "{app}\help"; Permissions: everyone-modify
Name: "{app}\sqldrivers"; Permissions: everyone-modify
Name: "{app}\imageformats"; Permissions: everyone-modify
Name: "{app}\translation"; Permissions: everyone-modify
Name: "{app}\itom-packages"; Permissions: everyone-modify
Name: "{app}\SDK"; Permissions: everyone-modify

[Languages]
Name: "Deutsch"; MessagesFile: "compiler:Languages\German.isl"
Name: "English"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "{#SourceDirITOM}\{#ExeName}"; DestDir: "{app}"; Flags: promptifolder

; DLLs
Source: "{#SourceDirQT}\phonon4.dll"; DestDir: "{app}"; Flags: promptifolder
;Source: "{#SourceDirQT}\Qt3Support4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtCLucene4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtCore4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtDeclarative4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtDesigner4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtDesignerComponents4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtGui4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtHelp4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtMultimedia4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtNetwork4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtOpenGL4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtScript4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtScriptTools4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtSql4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtSvg4.dll"; DestDir: "{app}"; Flags: promptifolder
;Source: "{#SourceDirQT}\QtTest4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtWebKit4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtXml4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\QtXmlPatterns4.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\qscintilla2.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirOpenCV}\opencv_core230.dll"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\..\plugins\sqldrivers\qsqlite4.dll"; DestDir: "{app}\sqldrivers"; Flags: promptifolder
Source: "{#SourceDirQT}\..\plugins\imageformats\qgif4.dll"; DestDir: "{app}\imageformats"; Flags: promptifolder
Source: "{#SourceDirQT}\..\plugins\imageformats\qico4.dll"; DestDir: "{app}\imageformats"; Flags: promptifolder
Source: "{#SourceDirQT}\..\plugins\imageformats\qicod4.dll"; DestDir: "{app}\imageformats"; Flags: promptifolder
Source: "{#SourceDirQT}\..\plugins\imageformats\qjpeg4.dll"; DestDir: "{app}\imageformats"; Flags: promptifolder
Source: "{#SourceDirQT}\..\plugins\imageformats\qmng4.dll"; DestDir: "{app}\imageformats"; Flags: promptifolder
Source: "{#SourceDirQT}\..\plugins\imageformats\qsvg4.dll"; DestDir: "{app}\imageformats"; Flags: promptifolder
Source: "{#SourceDirQT}\..\plugins\imageformats\qtga4.dll"; DestDir: "{app}\imageformats"; Flags: promptifolder
Source: "{#SourceDirQT}\..\plugins\imageformats\qtgad4.dll"; DestDir: "{app}\imageformats"; Flags: promptifolder
Source: "{#SourceDirQT}\..\plugins\imageformats\qtiff4.dll"; DestDir: "{app}\imageformats"; Flags: promptifolder

; Python-Scripts
Source: "{#SourceDirITOM}\itoDebugger.py"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirITOM}\itoFunctions.py"; DestDir: "{app}"; Flags: promptifolder

; Hilfe
; Source: "{#SourceDirITOMHelp}\documentation\itom_documentation\build\qthelp\*.*"; DestDir: "{app}\documentation\itom_documentation\build\qthelp"; Flags: promptifolder
Source: "{#SourceDirITOM}\docs\userDoc\build\qthelp\*.qch"; DestDir: "{app}\docs\userDoc\build\qthelp"; Flags: promptifolder
Source: "{#SourceDirQT}\assistant.exe"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\qhelpgenerator.exe"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\qcollectiongenerator.exe"; DestDir: "{app}"; Flags: promptifolder

; Sprachen
Source: "{#SourceDirITOM}\translation\*.qm"; DestDir: "{app}\translation"; Flags: promptifolder
Source: "{#SourceDirQT}\..\translations\qt_??.qm"; DestDir: "{app}\translation"; Flags: promptifolder

; EXEs
Source: "{#SourceDirQT}\assistant.exe"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceDirQT}\designer.exe"; DestDir: "{app}"; Flags: promptifolder
Source: "{#SourceRedistributable}\vcredist_x86.exe"; DestDir: "{tmp}"
Source: "{#SourceDirPython}\1. python-3.2.3.msi"; DestDir: "{tmp}"
Source: "{#SourceDirPython}\2. numpy-unoptimized-1.6.2.win32-py3.2.exe"; DestDir: "{tmp}"
Source: "{#SourceDirPython}\3. scipy-0.10.1-win32-superpack-python3.2.exe"; DestDir: "{tmp}"
Source: "{#SourceDirPython}\4. matplotlib-1.2.x.win32-py3.2.exe"; DestDir: "{tmp}"
Source: "{#SourceDirPython}\6. PIL-fork-1.1.7.win32-py3.2.exe"; DestDir: "{tmp}"

; vielleicht
Source: "{#SourceDirQT}\uic.exe"; DestDir: "{app}"; Flags: promptifolder  

; Default INI
Source: "{#SourceDirITOM}\itomSettings\itomDefault.ini"; DestDir: "{app}\itomSettings"; Flags: promptifolder  

; itom-packages
Source: "{#SourceDirITOM}\itom-packages\*.*"; DestDir: "{app}\itom-packages"; Flags: promptifolder  
Source: "{#SourceDirITOM}\itom-packages\api_generator\*.*"; DestDir: "{app}\itom-packages\api_generator"; Flags: promptifolder
Source: "{#SourceDirITOM}\itom-packages\abstractObjToolbar\*.*"; DestDir: "{app}\itom-packages\abstractObjToolbar"; Flags: promptifolder
;Source: "{#SourceDirITOM}\itom-packages\autostart\*.*"; DestDir: "{app}\itom-packages\autostart"; Flags: promptifolder
Source: "{#SourceDirITOM}\itom-packages\camToolbar\*.*"; DestDir: "{app}\itom-packages\camToolbar"; Flags: promptifolder
Source: "{#SourceDirITOM}\itom-packages\measurementSystem\*.*"; DestDir: "{app}\itom-packages\measurementSystem"; Flags: promptifolder
Source: "{#SourceDirITOM}\itom-packages\mpl_itom\*.*"; DestDir: "{app}\itom-packages\mpl_itom"; Flags: promptifolder
Source: "{#SourceDirITOM}\itom-packages\numpy_utils\*.*"; DestDir: "{app}\itom-packages\numpy_utils"; Flags: promptifolder
Source: "{#SourceDirITOM}\itom-packages\plotToolBar\*.*"; DestDir: "{app}\itom-packages\plotToolBar"; Flags: promptifolder
Source: "{#SourceDirITOM}\itom-packages\SurfaceCharakteristics\*.*"; DestDir: "{app}\itom-packages\SurfaceCharakteristics"; Flags: promptifolder
Source: "{#SourceDirITOM}\SDK\*.*"; DestDir: "{app}\SDK"; Flags: promptifolder
Source: "{#SourceDirITOM}\SDK\include\*.*"; DestDir: "{app}\SDK\include"; Flags: promptifolder
Source: "{#SourceDirITOM}\SDK\include\common\*.*"; DestDir: "{app}\SDK\include\common"; Components: SDK; Flags: promptifolder
Source: "{#SourceDirITOM}\SDK\include\DataObject\*.*"; DestDir: "{app}\SDK\include\DataObject"; Components: SDK;  Flags: promptifolder
Source: "{#SourceDirITOM}\SDK\include\PointCloud\*.*"; DestDir: "{app}\SDK\include\PointCloud"; Components: SDK;  Flags: promptifolder
Source: "{#SourceDirITOM}\SDK\include\QPropertyEditor\*.*"; DestDir: "{app}\SDK\include\QPropertyEditor"; Components: SDK;  Flags: promptifolder
Source: "{#SourceDirITOM}\SDK\lib\vc10_x86\*.*"; DestDir: "{app}\SDK\lib\vc10_x86"; Components: SDK;  Flags: promptifolder

[Icons]
Name: "{group}\{#Name}"; Filename: "{app}\{#ExeName}"
Name: "{commondesktop}\{#Name}"; Filename: "{app}\{#ExeName}"; Tasks: desktopicon

[Run]
Filename: "{tmp}\vcredist_x86.exe"; Flags: shellexec waituntilterminated; StatusMsg: "Microsoft Visual C++ 2010 Redistributable Package (x86) wird nun installiert!"; Components: CREDPACK;
Filename: "{tmp}\1. python-3.2.3.msi"; Flags: shellexec waituntilterminated; StatusMsg: "Phyton 3.2 wird nun installiert!"; Components: PYTHON
Filename: "{tmp}\2. numpy-unoptimized-1.6.2.win32-py3.2.exe"; Flags: shellexec waituntilterminated; StatusMsg: "NumPy wird nun installiert!"; Components: NUMPY
Filename: "{tmp}\3. scipy-0.10.1-win32-superpack-python3.2.exe"; Flags: shellexec waituntilterminated; StatusMsg: "SciPy wird nun installiert!"; Components: SCIPY
Filename: "{tmp}\4. matplotlib-1.2.x.win32-py3.2.exe"; Flags: shellexec waituntilterminated; StatusMsg: "MatPlotLib wird nun installiert!"; Components: MATPLOTLIB
Filename: "{tmp}\6. PIL-fork-1.1.7.win32-py3.2.exe"; Flags: shellexec waituntilterminated; StatusMsg: "Python Image Library wird nun installiert!"; Components: PIL
Filename: "{app}\{#ExeName}"; Flags: nowait postinstall skipifsilent; Description: "{cm:LaunchProgram,{#StringChange(Name, "&", "&&")}}"; Components: Main

[InstallDelete]
Type: files; Name: "{tmp}\1. python-3.2.3.msi"
Type: files; Name: "{tmp}\2. numpy-unoptimized-1.6.2.win32-py3.2.exe"
Type: files; Name: "{tmp}\3. scipy-0.10.1-win32-superpack-python3.2.exe"
Type: files; Name: "{tmp}\4. matplotlib-1.2.x.win32-py3.2.exe"
Type: files; Name: "{tmp}\6. PIL-fork-1.1.7.win32-py3.2.exe"
Type: files; Name: "{tmp}\vcredist_x86.exe"

[Types]
Name: "Install"; Description: "Installation"; Flags: iscustom

[Components]
Name: "Main"; Description: "iTOM"; Types: Install; Flags: checkablealone fixed
Name: "CREDPACK"; Description: "Microsoft Visual C++ 2010 Redistributable Package (x86)"; Types: Install; Flags: checkablealone fixed; Check: NeedInstallRP10
Name: "PYTHON"; Description: "Python 3.2.3"; Types: Install; Flags: checkablealone
Name: "NUMPY"; Description: "NumPy unoptimized 1.6.2 for Win32-Py 3.2"; Types: Install; Flags: checkablealone
Name: "SCIPY"; Description: "SciPy 0.10.1 RC2 Win32-Superpack-Py 3.2"; Types: Install; Flags: checkablealone
Name: "MATPLOTLIB"; Description: "MatPlotLib 1.2.x Win32 Py 3.2"; Types: Install; Flags: checkablealone
Name: "PIL"; Description: "Python Image Library 1.1.7 Win32 Py 3.2"; Types: Install; Flags: checkablealone
Name: "SDK"; Description: "itom SDK"; Types: Install; Flags: checkablealone

[UninstallDelete]
Type: filesandordirs; Name: "{app}\{#ExeName}"

[Code]
function MsiQueryProductState(ProductCode: string): integer;
  external 'MsiQueryProductStateA@msi.dll stdcall';

const
  INSTALLSTATE_DEFAULT = 5;
  INSTALLLEVEL_MAXIMUM = $ffff;
  INSTALLSTATE_ABSENT = 2;
  RP86 =    '{196BB40D-1578-3D01-B289-BEFC77A11A1E}';  // Visual C++ 2010 Redistributable Package (x86)
  RP64 =    '{DA5E371C-6333-3D8A-93A4-6FD5B20BCC6E}';  // Visual C++ 2010 Redistributable Package (x64)
  RPIA =    '{C1A35166-4301-38E9-BA67-02823AD72A1B}';  // Visual C++ 2010 Redistributable Package (ia64) 
  RP86SP1 = '{F0C3E5D1-1ADE-321E-8167-68EF0DE699A5}';  // Visual C++ 2010 SP1 Redistributable Package (x86)
  RP64SP1 = '{1D8E6291-B0D5-35EC-8441-6616F567A0F7}';  // Visual C++ 2010 SP1 Redistributable Package (x64)
  RPIASP1 = '{88C73C1C-2DE5-3B01-AFB8-B46EF4AB41CD}';  // Visual C++ 2010 SP1 Redistributable Package (ia64)

function IsMsiProductInstalled(const ProductId: string): Boolean;
begin
  Result:= MsiQueryProductState(ProductId) = INSTALLSTATE_DEFAULT;
end;

function NeedInstallRP10(): Boolean;
begin
  Result:= not (IsMsiProductInstalled(RP86) or IsMsiProductInstalled(RP86SP1));
//                IsMsiProductInstalled(RP64) or IsMsiProductInstalled(RP64SP1)
//                IsMsiProductInstalled(RPIA) or IsMsiProductInstalled(RPIASP1));
end;
